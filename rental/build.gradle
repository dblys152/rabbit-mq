plugins {
    id 'java'
    id 'org.springframework.boot' version '3.1.0'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'io.freefair.lombok' version '6.0.0-m2'
}

def javaProjects = [
        project(":rental:refs:product"),
        project(":rental:refs:user"),
        project(":rental:domain"),
        project(":rental:application"),
        project(":rental:adapter")

]
def springBootProjects = [
        project(":rental:application"),
        project(":rental:adapter")
]
def queryDslProjects = [
        project(":rental:adapter")
]

configure(javaProjects) {
    apply plugin: "java"
    apply plugin: "io.freefair.lombok"

    tasks.withType(JavaCompile) {
        sourceCompatibility = "17"
        targetCompatibility = "17"
        options.encoding = "UTF-8"
    }

    dependencies {
        implementation 'com.github.dblys152:shared:1.0.7'

        implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
        implementation 'org.hibernate:hibernate-validator:7.0.5.Final'
        implementation 'org.glassfish:jakarta.el:4.0.2'
        implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
        implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.15.2'
        implementation 'com.github.f4b6a3:tsid-creator:5.2.6'
        implementation 'org.apache.commons:commons-lang3:3.13.0'
        implementation 'commons-validator:commons-validator:1.7'

        if (!springBootProjects.contains(project)) {
            testImplementation(platform('org.junit:junit-bom:5.7.1'))
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
            testImplementation 'org.assertj:assertj-core:3.20.2'
            testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
        }
    }
}

configure(springBootProjects) {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
        implementation 'org.springframework.boot:spring-boot-starter-validation'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.springframework.boot:spring-boot-starter-amqp'

        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }
}

configure(queryDslProjects) {
    dependencies {
        implementation 'com.querydsl:querydsl-core:5.0.0'
        implementation 'com.querydsl:querydsl-jpa:5.0.0:jakarta'
        annotationProcessor 'com.querydsl:querydsl-apt:5.0.0:jakarta'
        annotationProcessor 'jakarta.persistence:jakarta.persistence-api'
        annotationProcessor 'jakarta.annotation:jakarta.annotation-api'
    }

    def querydslDir = "$buildDir/generated/querydsl"
    sourceSets {
        main.java.srcDirs += [ querydslDir ]
    }

    tasks.withType(JavaCompile) {
        options.annotationProcessorGeneratedSourcesDirectory = file(querydslDir)
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
        querydsl.extendsFrom compileClasspath
    }

    clean {
        delete file(querydslDir)
    }
}

bootJar {
    enabled(false)
}

jar {
    enabled(true)
}